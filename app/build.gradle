/*
* Copyright (C) 2020-2021 Sandip Vaghela
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*         http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'androidx.navigation.safeargs'
    id 'dagger.hilt.android.plugin'
}

kapt {
    correctErrorTypes = true
    useBuildCache = true
}

apply from: "$rootDir/gradle/oss-licence.gradle"
apply from: "$rootDir/gradle/apply-core.gradle"

hilt {
    enableAggregatingTask = true
}

ext {
    ci = System.getenv("CI") == "true"
}

android {
    compileSdkVersion SDK.COMPILE

    buildFeatures {
        viewBinding true
        dataBinding true
        compose true
    }

    defaultConfig {
        namespace 'com.afterroot.watchdone'
        applicationId "com.afterroot.watchdone"
        minSdkVersion SDK.MIN
        targetSdkVersion SDK.TARGET
        versionCode VERSION_CODE
        versionName VERSION_NAME
        vectorDrawables {
            useSupportLibrary = true
        }
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }

        testInstrumentationRunner "com.afterroot.watchdone.di.HiltTestRunner"

        manifestPlaceholders = [
                hostName  : "afterroot.web.app",
                pathPrefix: "/apps/watchdone/launch"
        ]

        resConfigs "en"

        def tmdbPropertiesFile = rootProject.file("tmdb.properties")
        def tmdbProperties = new Properties()
        if (tmdbPropertiesFile.exists()) {
            tmdbProperties.load(new FileInputStream(tmdbPropertiesFile))
        }
        buildConfigField "String", "TMDB_BEARER_TOKEN", tmdbProperties["tmdbBearerToken"] ?: System.getenv("TMDB_BEARER_TOKEN")
        buildConfigField "String", "TMDB_API", tmdbProperties["tmdbApi"] ?: System.getenv("TMDB_API")

        def commitHash = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = commitHash
        }

        def commit = System.getenv("COMMIT_ID") ?: commitHash.toString().trim()
        buildConfigField "String", "COMMIT_ID", "\"$commit\""
    }

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }

    signingConfigs {
        release {
            storeFile rootProject.file("release/keystore.jks")
            storePassword keystoreProperties['storePassword'] ?: System.getenv("SIGN_STORE_PW")
            keyAlias "watchdone"
            keyPassword keystoreProperties['keyPassword'] ?: System.getenv("SIGN_KEY_PW")
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            multiDexKeepProguard file('proguard-rules.pro')

            signingConfig signingConfigs.release
        }
        debug {
            ext.alwaysUpdateBuildId = false
            splits.abi.enable = false
            splits.density.enable = false
            crunchPngs false

            applicationIdSuffix '.debug'
            versionNameSuffix = '-beta'

            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        jniLibs {
            excludes += ['META-INF/proguard/*']
        }
        resources {
            excludes += ['META-INF/*.version', 'META-INF/proguard/*', '/*.properties', 'fabric/*.properties', 'META-INF/*.properties']
        }
    }


    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    lint {
        abortOnError false
    }

    composeOptions {
        println("- INFO: Compose Version: ${libs.versions.compose.get()}")
        println("- INFO: Compose Compiler Version: ${libs.versions.composeCompiler.get()}")
        kotlinCompilerExtensionVersion = libs.versions.composeCompiler.get()
    }
}

/*configurations.all {
    resolutionStrategy.force "org.antlr:antlr4-runtime:4.7.1"
    resolutionStrategy.force "org.antlr:antlr4-tool:4.7.1"
}*/

dependencies {

    implementation project(':base')
    implementation project(':data')
    implementation project(':ui:common')
    implementation project(':ui:common-compose')
    implementation project(':ui:discover')
    implementation project(':ui:media')
    implementation project(':ui:resources')
    implementation project(':ui:watchlist')

    implementation libs.kotlin.stdLib

    implementation libs.androidx.appCompat
    implementation libs.androidx.activity
    implementation libs.androidx.billing
    implementation libs.androidx.cardView
    implementation libs.androidx.constraintLayout
    implementation libs.androidx.core
    implementation libs.androidx.fragment
    implementation libs.bundles.lifecycle
    implementation libs.androidx.multiDex
    implementation libs.androidx.navigation.fragment
    implementation libs.androidx.navigation.ui
    implementation libs.androidx.paging
    implementation libs.androidx.palette
    implementation libs.androidx.preference
    implementation libs.androidx.recyclerView
    implementation libs.androidx.startUp
    implementation libs.androidx.supportV13
    implementation libs.androidx.supportV4
    implementation libs.androidx.vectorDrawable

    implementation libs.bundles.compose
    debugImplementation libs.androidx.compose.tooling

    implementation libs.materialdialogs.input
    implementation libs.materialdialogs.core
    implementation libs.materialdialogs.bottomSheets
    implementation libs.materialdialogs.color

    implementation libs.firebase.ui.auth
    implementation libs.firebase.ui.firestore
    implementation libs.firebase.ui.storage

    implementation libs.hilt.hilt
    implementation libs.hilt.compose
    kapt libs.hilt.compiler

    kapt libs.androidx.room.compiler

    implementation libs.google.ossLic
    implementation libs.google.material
    implementation libs.bundles.firebase

    implementation libs.commonsIo

    implementation libs.kotlin.coroutines.android
    testImplementation libs.kotlin.coroutines.test

    implementation libs.materialProgress

    implementation libs.okhttp.okhttp
    implementation libs.okhttp.logging
    implementation libs.retrofit.retrofit
    implementation libs.retrofit.jackson

    testImplementation libs.test.junit
    androidTestImplementation libs.androidx.test.junitExt
    androidTestImplementation libs.androidx.test.espresso

    testImplementation libs.androidx.test.core
    testImplementation 'org.mockito:mockito-core:4.8.1'
    androidTestImplementation 'org.mockito:mockito-android:4.8.0'

    implementation 'androidx.work:work-runtime-ktx:2.7.1'
    implementation libs.google.auth

    androidTestImplementation libs.androidx.test.archCore
    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.hilt.testing
    androidTestImplementation libs.kotlin.coroutines.test
    androidTestImplementation libs.test.mockk
    androidTestImplementation libs.test.robolectric
}
