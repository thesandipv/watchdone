/*
 * Copyright (C) 2020-2021 Sandip Vaghela
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

apply from: "$rootDir/gradle/common-config.gradle"
//apply from: "$rootDir/gradle/compose.gradle" //Uncomment for Enabling Compose

android {
    defaultConfig {
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def tmdbPropertiesFile = rootProject.file("tmdb.properties")
        def tmdbProperties = new Properties()
        if (tmdbPropertiesFile.exists()) {
            tmdbProperties.load(new FileInputStream(tmdbPropertiesFile))
        }
        buildConfigField "String", "TMDB_BEARER_TOKEN", tmdbProperties["tmdbBearerToken"] ?: System.getenv("TMDB_BEARER_TOKEN")
        buildConfigField "String", "TMDB_API", tmdbProperties["tmdbApi"] ?: System.getenv("TMDB_API")
    }
}

dependencies {
    api project(':tmdbapi')
    implementation project(':base')
    implementation project(':ui:resources')
    api project(':data_structures:lib')

    implementation libs.androidx.preference
    api libs.androidx.datastore

    api libs.google.gson

    api libs.firebase.firestore
    api libs.firebase.auth
    api libs.firebase.config
    api libs.firebase.messaging

    api libs.kotlin.coroutines.core
    api libs.kotlin.coroutines.android
    api libs.kotlin.coroutines.play

    api libs.androidx.room.room
    api libs.androidx.room.runtime
    kapt libs.androidx.room.compiler

    implementation libs.hilt.hilt
    kapt libs.hilt.compiler

    testImplementation libs.androidx.room.test
    testImplementation libs.androidx.test.archCore
    testImplementation libs.androidx.test.core
    testImplementation libs.hilt.testing
    testImplementation libs.kotlin.coroutines.test
    testImplementation libs.test.mockk
    testImplementation libs.test.junit
    testImplementation libs.androidx.test.junitExt
    testImplementation libs.test.robolectric

    kaptTest libs.hilt.compiler
    kaptTest libs.androidx.room.compiler

    implementation libs.coil
}
